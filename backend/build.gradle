plugins {
	id 'org.springframework.boot' version '2.7.1'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'jacoco'
	id "com.diffplug.spotless" version "6.9.1"
	id 'checkstyle'
}

group = 'com.moloko'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

// disable generation of thin JAR as we use SpringBoot jar
jar {
	enabled = false
}

dependencies {
	annotationProcessor 'com.github.therapi:therapi-runtime-javadoc-scribe:0.13.0'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springdoc:springdoc-openapi-webflux-ui:1.6.9'
	implementation 'org.springdoc:springdoc-openapi-javadoc:1.6.9'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.mockito:mockito-inline'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

tasks.named('test') {
	useJUnitPlatform()
}

/*----------------------------------JACOCO CONFIG--------------------------------*/
def excludes = [
	'com/moloko/molokoblogengine/model/**',
	'com/moloko/molokoblogengine/MolokoBlogEngineApplication.*',
	'com/moloko/molokoblogengine/config/ApplicationSecurityConfig*',
	'com/moloko/molokoblogengine/config/SwaggerConfig.*'
]

private excludeJacoco(classDirectories, excludes) {
	classDirectories.setFrom(files(classDirectories.files.collect {
		fileTree(dir: it, exclude: excludes)
	}))
}

jacocoTestReport {
	excludeJacoco(classDirectories, excludes)
}

jacocoTestCoverageVerification {
	excludeJacoco(classDirectories, excludes)
	violationRules {
		rule {
			limit {
				counter = 'LINE'
				minimum = 1.0
			}
		}
	}
}
check.dependsOn jacocoTestCoverageVerification

spotless {
	java {
		googleJavaFormat()
	}
	groovyGradle {
		greclipse()
	}
}

checkstyle {
	toolVersion '10.3.2'
	configFile 'checkstyle.xml' as File
}
